OBJECT Codeunit 6 Fiscal Year-Close
{
  OBJECT-PROPERTIES
  {
    Date=21.11.19;
    Time=10:02:47;
    Modified=Yes;
    Version List=NAVW13.00;
  }
  PROPERTIES
  {
    TableNo=50;
    OnRun=BEGIN
            AccountingPeriod.COPY(Rec);
            Code;
            Rec := AccountingPeriod;
          END;

  }
  CODE
  {
    VAR
      Text001@1000 : TextConst 'ENU=You must create a new fiscal year before you can close the old year.;NOR=Du m† opprette et nytt regnskaps†r f›r du kan lukke det gamle †ret.';
      Text002@1001 : TextConst 'ENU="This function closes the fiscal year from %1 to %2. ";NOR="Denne funksjonen lukker regnskaps†ret fra %1 til %2. "';
      Text003@1002 : TextConst 'ENU=Once the fiscal year is closed it cannot be opened again, and the periods in the fiscal year cannot be changed.\\;NOR=N†r regnskaps†ret er lukket kan det ikke †pnes igjen, og periodene i regnskaps†ret kan ikke lenger endres.\\';
      Text004@1003 : TextConst 'ENU=Do you want to close the fiscal year?;NOR=Vil du lukke regnskaps†ret?';
      AccountingPeriod@1004 : Record 50;
      AccountingPeriod2@1005 : Record 50;
      AccountingPeriod3@1006 : Record 50;
      FiscalYearStartDate@1007 : Date;
      FiscalYearEndDate@1008 : Date;

    LOCAL PROCEDURE Code@1();
    BEGIN
      WITH AccountingPeriod DO BEGIN
        AccountingPeriod2.SETRANGE(Closed,FALSE);
        AccountingPeriod2.FIND('-');

        FiscalYearStartDate := AccountingPeriod2."Starting Date";
        AccountingPeriod := AccountingPeriod2;
        TESTFIELD("New Fiscal Year",TRUE);

        AccountingPeriod2.SETRANGE("New Fiscal Year",TRUE);
        IF AccountingPeriod2.FIND('>') THEN BEGIN
          FiscalYearEndDate := CALCDATE('<-1D>',AccountingPeriod2."Starting Date");

          AccountingPeriod3 := AccountingPeriod2;
          AccountingPeriod2.SETRANGE("New Fiscal Year");
          AccountingPeriod2.FIND('<');
        END ELSE
          ERROR(Text001);

        IF NOT
           CONFIRM(
             Text002 +
             Text003 +
             Text004,FALSE,
             FiscalYearStartDate,FiscalYearEndDate)
        THEN
          EXIT;

        RESET;

        SETRANGE("Starting Date",FiscalYearStartDate,AccountingPeriod2."Starting Date");
        MODIFYALL(Closed,TRUE);

        SETRANGE("Starting Date",FiscalYearStartDate,AccountingPeriod3."Starting Date");
        MODIFYALL("Date Locked",TRUE);

        RESET;
      END;
    END;

    BEGIN
    {
      // Dette er en test
    }
    END.
  }
}

